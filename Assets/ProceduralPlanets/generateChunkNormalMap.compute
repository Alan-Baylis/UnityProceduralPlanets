#pragma kernel main

#include "planet.compute.cginc"

Texture2D<float4> _chunkSlopeAndCurvatureMap;
SamplerState sampler_chunkSlopeAndCurvatureMap;

Texture2D<float> _chunkHeightMap;
SamplerState sampler_chunkHeightMap;

float _heightMin;
float _heightMax;


RWTexture2D<float4> _chunkNormalMap;


float2 getSlope(int2 idXy)
{
	int w, h;
	_chunkHeightMap.GetDimensions(w, h);
	int3 off = int3(-1, 0, 1);
	int2 s11pos = idXy;
	s11pos = max(s11pos, int2(1, 1));
	s11pos = min(s11pos, int2(w - 2, h - 2));
	// s00 __ s10 __ s20
	//  |      |      |
	// s01 __ s11 __ s21
	//  |      |      |
	// s02 __ s12 __ s22
	float s00 = _chunkHeightMap[s11pos + off.xx].r;
	float s10 = _chunkHeightMap[s11pos + off.yx].r;
	float s20 = _chunkHeightMap[s11pos + off.zx].r;

	float s01 = _chunkHeightMap[s11pos + off.xy].r;
	float s11 = _chunkHeightMap[s11pos].r;
	float s21 = _chunkHeightMap[s11pos + off.zy].r;

	float s02 = _chunkHeightMap[s11pos + off.xz].r;
	float s12 = _chunkHeightMap[s11pos + off.yz].r;
	float s22 = _chunkHeightMap[s11pos + off.zz].r;

	// modified https://en.wikipedia.org/wiki/Sobel_operator
	//-1 __ 0 __ 1
	// |    |    |
	//-2 __ 0 __ 2
	// |    |    |
	//-1 __ 0 __ 1
	float sx = (s20 - s00) + (s21 - s01) * 2 + (s22 - s02);
	//-1 __-2 __-1
	// |    |    |
	// 0 __ 0 __ 0
	// |    |    |
	// 1 __ 2 __ 1
	float sy = (s02 - s00) + (s12 - s10) * 2 + (s22 - s20);

	return float2(sx, sy);
}

[numthreads(16, 16, 1)]
void main(uint3 id : SV_DispatchThreadID)
{
	float2 slope = _chunkSlopeAndCurvatureMap[id.xy].xy;
	//float2 slope = getSlope(id.xy);

	float3 normal;
	normal = cross(
		normalize(float3(1, 0, slope.x)),
		normalize(float3(0, 1, slope.y))
	);
	//normal = float3(slope.x, slope.y, 1);

	normal = normalize(normal);

	normal = PACK_NORMAL(normal);

	//DEBUG
	//normal = float3(0.5, 0.5, 1);

	_chunkNormalMap[id.xy] = float4(normal, 1);

}